/*
 * Main.s
 *
 *  Created on: Jan 12, 2024
 *      Author: rozman
 */

		  .syntax unified
		  .cpu cortex-m7
		  .thumb


///////////////////////////////////////////////////////////////////////////////
// Definitions
///////////////////////////////////////////////////////////////////////////////
// Definitions section. Define all the registers and
// constants here for code readability.

// Constants

// Register Addresses
	.equ     DWT_CYCCNT,   	0xE0001004 // DWT_CYCCNT reg (RM, pp.3211)

// Vector table offset register definition
// Important for relocated Vector table on running from RAM
	.equ VTOR,0xE000ED08

//-----------------------------
// GPIOs Register definitions
//-----------------------------

// Register Addresses
// You can find the base addresses for all peripherals from Memory Map section 2.3.2
// RM0433 on page 131. Then the offsets can be found on their relevant sections.

// RCC   base address is 0x58024400
//   AHB4ENR register offset is 0xE0
	.equ     RCC_AHB4ENR,   0x580244E0 // RCC AHB4 peripheral clock reg

// GPIOA base address is 0x58020000
	.equ     GPIOA_BASE,   0x58020000 // GPIOI base address)

// GPIOI base address is 0x58022000
	.equ     GPIOI_BASE,   0x58022000 // GPIOI base address)

// GPIOJ base address is 0x58022000
	.equ     GPIOJ_BASE,   0x58022400 // GPIOJ base address)

//   MODER register offset is 0x00
	.equ     GPIOx_MODER,   0x00 // GPIOx port mode register
//   ODR   register offset is 0x14
	.equ     GPIOx_ODR,     0x14 // GPIOx output data register
//   BSSR   register offset is 0x18
	.equ     GPIOx_BSRR,     0x18 // GPIOx port set/reset register


// Values for BSRR register - pin 13: LED is on, when GPIO is off
	.equ     LED2_OFF,       0x00002000   	// Setting pin to 1 -> LED is off
	.equ     LED2_ON,   	 0x20000000   	// Setting pin to 0 -> LED is on

// Values for BSRR register - pin 2: LED is on, when GPIO is off
	.equ     LED1_OFF,       0x00000004   	// Setting pin to 1 -> LED is off
	.equ     LED1_ON,   	 0x00040000   	// Setting pin to 0 -> LED is on

// Values for BSRR register - pin 3: PA3
	.equ     PA3_ON,         0x00000008   	// Setting pin to 1
	.equ     PA3_OFF,        0x00080000   	// Setting pin to 0

   	.equ     MSDELAY,       64000

//-----------------------------
// SysTick Timer definitions
//-----------------------------

	.equ     SCS_BASE,0xe000e000
	.equ     SCS_SYST_CSR,0x10// Control/Status register
	.equ     SCS_SYST_RVR,0x14// Value to countdown from
	.equ     SCS_SYST_CVR,0x18// Current value

	.equ	 SYSTICK_RELOAD_1MS,	63999  //1 msec at 64MHz ...

//-----------------------------
// USART3 related definitions
//-----------------------------

// RCC  base address is 0x58024400
	.equ    RCC_BASE,  0x58024400 // RCC base reg

//  APB1LENR register offset is 0xE8
	.equ    RCC_APB1LENR,  0xE8 // RCC APB1LENR peripheral clock reg

// GPIOB base address is 0x58020400
	.equ    GPIOB_BASE,  0x58020400 // GPIOB base address)

//  AFRH  register offset is 0x24
	.equ    GPIOx_AFRH,    0x24 //
	.equ    GPIO_AFRH_VALUE,    0x7700 // AF7 on PB10,11

// USART3 base address is 0x40004800
	.equ    USART3_BASE,  0x40004800 // USART3 base address)

//  CRx registers
//          CR1 register
	.equ    USART_CR1,  0x00 // CR1 register
	.equ    USART_CR1_VAL,  0b1101 // CR1 register value

	.equ    USART_CR2,  0x04 // CR2 register
	.equ    USART_CR3,  0x08 // CR3 register

//  BRR register
	.equ    USART_BRR,  0x0C // BRR register
	.equ    USART_BRR_VAL,556 // BRR register 64 000 000 / 115 200 = 555.55  = 556

//  ISR register
	.equ    USART_ISR,    0x1c // ISR register

//  Data registers
	.equ    USART_RDR,    0x24 // Receive Data Register
	.equ    USART_TDR,    0x28 // Transmit Data Register

//-----------------------------
// ITM Trace related definitions
//-----------------------------
// ITM Registers definitions
	.equ    M7_ITM_BASE, 0xE0000000

	.equ    M7_ITM_STIM0, 0x00
	.equ    M7_ITM_STIM1, 0x04

	.equ    M7_ITM_TER, 0xE00  // Enable Channels (0,1)
	.equ    M7_ITM_TCR, 0xE80  // Bit0 to 1 - enable ITM


// Comment following line when FLASH Linker Script is used
#define RAM_LinkScript


// Start of data section
 		.data

 		.align
// -------------------------------
// Mini RTOS related variables
// -------------------------------


	.equ STACKSIZE , 240 // + 16 registers of PCB

USERSTACK_Start: .space (STACKSIZE+16) * 4
USERSTACK_End:


STACK0_Start: 	.space 		STACKSIZE * 4
// PCB: ISR saves: R4-R11 (8regs), CPU saves: R0-R3, R12,      LR,        PC,         PSR
PCB0:    		.word     	4,5,6,7,8,9,10,11,        0,1,2,3, 12,  0xFFFFFFFF,   TASK0_Start, 0x01000000
STACK0_End:

STACK1_Start: 	.space 		STACKSIZE * 4
PCB1:    		.word     	4,5,6,7,8,9,10,11,        0,1,2,3, 12,  0xFFFFFFFF,   TASK1_Start, 0x01000000
STACK1_End:

StackPointer0:	.word     	PCB0
StackPointer1:  .word     	PCB1

ACT_TASKID: 	.word     	0         // current process ID (0,1,...)


// Start of text section
  		.text

  		.type  main, %function
  		.global main

   	   	.align
main:

#ifdef RAM_LinkScript

// Relocating Vector table to RAM (only for RAM Linker Script)
        bl RELLOC_VECTBL

#endif



		bl INIT_GPIOs

   		bl INIT_USART3 // Priprava USART3 naprave

		bl ITM_Init    // Priprava ITM trace kanala

// ----------------------------
//		MiniRTOS_Init
// ----------------------------

        // Set new User SP that will be used for user program (MSP is reset default)
        ldr r0,=USERSTACK_End-4
        msr psp, r0

		// Set PSP as SP when stacking/unstacking user processes
		// Access special registers
		mrs  r0, CONTROL   // Read CONTROL into R0
		orr  r0, R0, #0x2  // Set SPSEL (PSP to be used in Thread mode) to select PSP
		msr  CONTROL, r0   // Write R0 into CONTROL
		isb                // Instruction Synchronization Barrier - needed

        CPSID   I          // Disable interrupts

    	bl INIT_TC_PSP     // Initialize SysTick periodic interrupt (every msec)

    	b  TASK0_Start     // Go into first task (it will switch with TASK1 every msec)

__end: 	b 	__end

.global SysTick_Handler
.section .text.SysTick_Handler,"ax",%progbits
.type SysTick_Handler, %function

SysTick_Handler:
	/// STEP 1 - SAVE THE CURRENT TASK CONTEXT
	/// At this point the processor has already pushed PSR, PC, LR, R12, R3, R2, R1 and R0
	/// onto the stack. We need to push the rest(i.e R4, R5, R6, R7, R8, R9, R10 & R11) to save the
	/// context of the current task. PSP must be used !

	/// Disable interrupts
        CPSID   I

    /// Push remaining registers R4 to R11
	//	push {r4-r11} - not allowed, it would use MSP !!!
		 // Note: we cannot use PUSH {r7-r9} because PUSH uses MSP
		 // in a interrupt service routine
		mrs r3, psp       	// if 1, PSP was used
        stmfd r3!, {r4-r11} // Push onto a Full Descending Stack

	    ldr r0,=ACT_TASKID    // Check current Active task ID
		ldr r1,[r0]
		cmp r1,#0
		bne CONT1

		// TASK 0 current
		mov r2,#1             // Task1 is new
		str r2,[r0]

		ldr r0,=StackPointer0  // Store current PSP
		str r3,[r0]

		ldr r0,=PCB1
		ldr r1,=StackPointer1
		b   STEP2

		// TASK 1 current

CONT1:
		mov r2,#0            // Task0 is new
		str r2,[r0]

		ldr r0,=StackPointer1  // Store current PSP
		str r3,[r0]

		ldr r0,=PCB0
		ldr r1,=StackPointer0

    /// STEP 2: LOAD THE NEW PSP and NEW TASK CONTEXT
    /// FROM ITS STACK TO THE CPU REGISTERS.

STEP2:

		ldr r3,[r1]  // Read new PSP into r3

	    /// Pop registers R4-R11
//	    pop {r4 - r11}       - not allowed, it would use MSP !!!
        ldmfd r3!, {r4-r11}  // Load from a Full Descending Stack

        msr psp, r3          // move to PSP for upcoming task

	/// Enable interrupts
	    CPSIE   I

	    BX      LR

TASK0_Start:



T0Loop:
		bl  LED1_ON
		mov r0,#0
		mov r1,#'1'
		bl  ITM_Send

        CPSIE   I  // Enable interrupts (context switches will happen !

 		mov r1,#'0'
		bl  SEND_UART

		mov r0,#500
		bl 	DELAY // Zakasnitev SW Delay: r0 x 1msec

		bl  LED1_OFF
		mov r0,#0
		mov r1,#'0'
		bl  ITM_Send

		mov r0,#500
		bl 	DELAY // Zakasnitev SW Delay: r0 x 1msec

		b 	T0Loop

TASK1_Start:

T1Loop:
		bl  LED2_ON
		mov r0,#1
		mov r1,#'1'
		bl  ITM_Send

 		mov r1,#'1'
		bl  SEND_UART

		mov r0,#500
		bl 	DELAY // Zakasnitev SW Delay: r0 x 1msec

		bl  LED2_OFF
		mov r0,#1
		mov r1,#'0'
		bl  ITM_Send

		mov r0,#500
		bl 	DELAY // Zakasnitev SW Delay: r0 x 1msec

		b 	T1Loop


INIT_GPIOs:

  	push {r5, r6, lr}

	// Enable GPIOA,I,J Peripheral Clock (bit 8 in AHB4ENR register)
	ldr r6, = RCC_AHB4ENR       // Load peripheral clock reg address to r6
	ldr r5, [r6]                // Read its content to r5
	orr r5, #0x00000300         // Set bits 8 and 9 to enable GPIOI,J clock
	orr r5, #0x00000001         // Set bits 1 to enable GPIOA clock
	str r5, [r6]                // Store result in peripheral clock register

	// Make GPIOA Pin3 as output pin (bits 7:6 in MODER register)
	ldr r6, =GPIOA_BASE       // Load GPIOA BASE address to r6
	ldr r5, [r6,#GPIOx_MODER]  // Read GPIOA_MODER content to r5
	and r5, #0xFFFFFF3F          // Clear bits 7-6 for PA3
	orr r5, #0x00000040          // Write 01 to bits 7-6 for PA3
	str r5, [r6]                // Store result in GPIO MODER register

	// Make GPIOI Pin13 as output pin (bits 27:26 in MODER register)
	ldr r6, =GPIOI_BASE       // Load GPIOI BASE address to r6
	ldr r5, [r6,#GPIOx_MODER]  // Read GPIOI_MODER content to r5
	and r5, #0xF3FFFFFF          // Clear bits 27-26 for P13
	orr r5, #0x04000000          // Write 01 to bits 27-26 for P13
	str r5, [r6]                // Store result in GPIO MODER register

	// Make GPIOJ Pin2 as output pin (bits 5:4 in MODER register)
	ldr r6, =GPIOJ_BASE       // Load GPIOJ BASE address to r6
	ldr r5, [r6,#GPIOx_MODER]  // Read GPIOJ_MODER content to r5
	and r5, #0xFFFFFFCF          // Clear bits 5-4 for P2
	orr r5, #0x00000010          // Write 01 to bits 5-4 for PJ2
	str r5, [r6]                // Store result in GPIO MODER register

  	pop {r5, r6, pc}

LED1_ON:
		push {r5,r6,lr}

		ldr r6, =GPIOJ_BASE       // Load GPIOI BASE address to r6
		// Led On
//		mov    r5, #LED1_ON
		mov    r5, #0x00040000
 		str    r5, [r6,#GPIOx_BSRR] // Write to BSRR register

		pop {r5,r6,pc}

LED1_OFF:
		push {r5,r6,lr}

		ldr r6, =GPIOJ_BASE       // Load GPIOI BASE address to r6
		// Led Off
//		mov    r5, #LED1_OFF
		mov    r5, #0x00000004
 		str    r5, [r6,#GPIOx_BSRR] // Write to BSRR register

		pop {r5,r6,pc}

LED2_ON:
		push {r5,r6,lr}

		ldr r6, =GPIOI_BASE       // Load GPIOI BASE address to r6
		// Led On
//		mov    r5, #LED2_ON
		mov    r5, #0x20000000
 		str    r5, [r6,#GPIOx_BSRR] // Write to BSRR register

		pop {r5,r6,pc}


LED2_OFF:
		push {r5,r6,lr}

		ldr r6, =GPIOI_BASE       // Load GPIOI BASE address to r6
		// Led Off
//		mov    r5, #LED2_OFF
		mov    r5, #0x00002000
 		str    r5, [r6,#GPIOx_BSRR] // Write to BSRR register

		pop {r5,r6,pc}



PA3_ON:
		push {r5,r6,lr}

		ldr r6, =GPIOA_BASE       // Load GPIOI BASE address to r6
		// Led On
//		mov    r5, #PA3_ON
 		mov    r5, #0x00000008
 		str    r5, [r6,#GPIOx_BSRR] // Write to BSRR register

		pop {r5,r6,pc}


PA3_OFF:
		push {r5,r6,lr}

		ldr r6, =GPIOA_BASE       // Load GPIOI BASE address to r6
		// Led Off
//		mov    r5, #PA3_OFF
		mov    r5, #0x00080000
 		str    r5, [r6,#GPIOx_BSRR] // Write to BSRR register

		pop {r5,r6,pc}

ITM_Init:

  	push {r0, r1, lr}

    ldr r0, =M7_ITM_BASE // Base address
    mov r1, #0b11
    str r1,[r0,#M7_ITM_TER]

    mov r1, #1
    str r1,[r0,#M7_ITM_TCR]

  	pop {r0, r1, pc}

ITM_Send:

  	push {r2, lr}

	ldr r2, =M7_ITM_BASE // Base address

    cmp r0,#0 // Channel 0 ?
    bne Ch1
    str r1,[r2]
    b   contx
Ch1:
	str r1,[r2,#4]

contx:

  	pop {r2, pc}

RELLOC_VECTBL:

  	push {r0, r1, lr}

    ldr r1, =VTOR // Set Vector table addr. to 0x24000000
	ldr r0, =0x24000000
	str r0, [r1]

  	pop {r0, r1, pc}

INIT_USART3:
  		push {r0, r1, r2, lr}

		// Enable USART3 Peripheral Clock (bit 18 in APB1LENR register)
		ldr r1, =RCC_BASE          // Load peripheral clock reg base address to r1
		ldr r0, [r1,#RCC_APB1LENR]  // Read its content to r0
		orr r0, r0, #(1<<18)          // Set bit 18 to enable USART3 clock
		str r0, [r1,#RCC_APB1LENR]  // Store result in peripheral clock register

		// Enable GPIOB Peripheral Clock (bit 1 in AHB4ENR register)
		ldr r1, = RCC_AHB4ENR      // Load peripheral clock reg address to r6
		ldr r0, [r1]                // Read its content to r5
		orr r0, r0, #0b10              // Set bit 1 to enable GPIOB clock
		str r0, [r1]                // Store result in peripheral clock register

		ldr r1, =GPIOB_BASE          // Load GPIOB BASE address to r1

		// Make GPIOB Pina 10,11 as AF (bits 20:23 in MODER register)
		ldr r0, [r1,#GPIOx_MODER]  // Read GPIO_MODER content to r0
		ldr r2, =0xFF0FFFFF          // Clear mask
		and r0, r0, r2                  // Clear bits
		orr r0, #0x00A00000          // Write 10 to bits
		str r0, [r1,#GPIOx_MODER]    // Store result in GPIO MODER register

		// Make GPIOB Pina 10,11 as AF7 (bits 8:15 in AFRH register)
		ldr r0, [r1,#GPIOx_AFRH]  // Read GPIOB AFRH content to r0
		ldr r2, =0xFFFF00FF          // Clear mask
		and r0, r0, r2              // Clear bits
		orr r0, r0, #GPIO_AFRH_VALUE
		str r0, [r1,#GPIOx_AFRH]  // Store result in GPIOB AFRH register


		ldr r1, =USART3_BASE      // Load USART3 BASE address to r1

		  // Disable USART3
		mov r0, #0
		str r0, [r1,#USART_CR1]                // Store result

		// Set USART3 BaudRate
		ldr r0, =USART_BRR_VAL
		str r0, [r1,#USART_BRR]                // Store result

		// Start USART3
		mov r0, #USART_CR1_VAL
		str r0, [r1,#USART_CR1]                // Store result

  		pop {r0, r1, r2, pc}

RECV_UART:
  		push {r1, r2, lr}

      	ldr r1, =USART3_BASE
RECV_LP:
	    ldr r2, [r1, #USART_ISR]
	    tst r2, #(1 << 5)  // RXNE flag
	    beq RECV_LP

	    ldr r0, [r1, #USART_RDR]

  		pop {r1, r2, pc}


SEND_UART:
		push {r1, r2, lr}

		ldr r1, =USART3_BASE
SEND_LP:
        ldr r2, [r1, #USART_ISR]
	    tst r2, #(1 << 7)  // TXE flag
	    beq SEND_LP

	    str r0, [r1, #USART_TDR]

	    pop {r1, r2, pc}

INIT_TC_PSP:
	  	push {r0, r1, lr}
		ldr r1, =SCS_BASE

		ldr r0, =SYSTICK_RELOAD_1MS
		str r0, [r1, #SCS_SYST_RVR]

		mov r0, #0
		str r0, [r1, #SCS_SYST_CVR]

		mov r0, #0b111    // Set TickInt to 1 as well
		str r0, [r1, #SCS_SYST_CSR]

	  	pop {r0, r1, pc}


DELAY:
		push {r1,lr}

MSEC:   ldr r1,=MSDELAY
LOOP:   	subs r1,r1,#1
	        bne  LOOP

		subs r0,r0,#1
		bne  MSEC

		pop {r1,pc}

